# Maintainer: Wouter Wijsman <wwijsman@live.nl>
pkgname=libyaml
pkgver=0.1.4
pkgrel=1
pkgdesc="LibYAML is a YAML parser and emitter library"
arch=('mips')
url="https://pyyaml.org/wiki/LibYAML"
license=('MIT')
depends=()
makedepends=()
optdepends=()
group=('all')
source=(
    "http://pyyaml.org/download/libyaml/yaml-${pkgver}.tar.gz"
    "https://raw.githubusercontent.com/pspdev/psplibraries/c4746da120f66f08e07d714334fcf557e259f6cc/patches/config.sub"
)
sha256sums=(
    '7bf81554ae5ab2d9b6977da398ea789722e0db75b86bffdaeb4e66d961de6a37'
    '4f39430baff745be9a2518d53d92c0612a298526e20dab259cdf0f62d60489ab'
)

prepare() {
    cd "yaml-$pkgver"
    cp ../config.sub config/config.sub
}

build() {
    cd "yaml-$pkgver"
    LDFLAGS="$LDFLAGS -L$(psp-config --pspsdk-path)/lib -L$(psp-config --psp-prefix)/lib -lc -lpspuser" \
    LIBS="$LIBS -lc -lpspuser" \
    ./configure --host=psp --prefix=/psp
    make
}

package() {
    cd "yaml-$pkgver"
    make DESTDIR="$pkgdir/" install

    mkdir -m 755 -p "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 LICENSE "$pkgdir/psp/share/licenses/$pkgname"
}

