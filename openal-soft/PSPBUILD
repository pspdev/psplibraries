# Maintainer: Wouter Wijsman <wwijsman@live.nl>
pkgname=openal-soft
pkgver=1.14
pkgrel=1
pkgdesc="OpenAL Soft is an LGPL-licensed, cross-platform, software implementation of the OpenAL 3D audio API"
arch=('mips')
url="https://openal-soft.org/"
license=('LGPL')
depends=('psp-cmake' 'pthread-embedded')
makedepends=()
optdepends=()
provides=('openal')
source=(
    "http://openal-soft.org/openal-releases/${pkgname}-${pkgver}.tar.bz2"
    "${pkgname}-${pkgver}-PSP.patch"
)
sha256sums=(
    '87bd8d61d5943387898c92b6a2bbbb26118e745dec57550c817526a70fad0914'
    'SKIP'
)

prepare() {
    cd "$pkgname-$pkgver"
    patch -p1 < ../${pkgname}-${pkgver}-PSP.patch
}

build() {
    cd "$pkgname-$pkgver"
    psp-cmake -D CMAKE_INSTALL_PREFIX="/psp" -D UTILS=OFF -D EXAMPLES=OFF .
    make
}

package() {
    cd "$pkgname-$pkgver"
    make DESTDIR="${pkgdir}" install

    mkdir -m 755 -p "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 COPYING "$pkgdir/psp/share/licenses/$pkgname"
}

