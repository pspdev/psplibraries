# Maintainer: Wouter Wijsman <wwijsman@live.nl>
pkgname=pspgl
pkgver=r64.cd54f7e
pkgrel=1
pkgdesc="OpenGL-ESish library for PSP"
arch=('mips')
url="https://github.com/pspdev/psp-ports"
license=('BSD')
depends=()
makedepends=()
optdepends=()
source=("git+https://github.com/pspdev/psp-ports.git")
sha256sums=('SKIP')

pkgver() {
  cd psp-ports
  # Set the revisioin of the repo as version
  # Means any update to psp-ports will result in a new version number
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "psp-ports/$pkgname"
	make
}

package() {
	cd "psp-ports/$pkgname"
	# make install isn't usable for pspgl, so we're installing manually
	mkdir -m 755 -p "$pkgdir/psp/lib" "$pkgdir/psp/include"
	install -Dm 644 "libGL.a" "$pkgdir/psp/lib/libGL.a"
	install -Dm 644 "libGLU.a" "$pkgdir/psp/lib/libGLU.a"
	install -Dm 644 "libglut.a" "$pkgdir/psp/lib/libglut.a"

	mkdir -m 755 -p "$pkgdir/psp/include/GL/" "$pkgdir/psp/include/GLES/"
	install -m 644 GL/* "$pkgdir/psp/include/GL/"
	install -m 644 GLES/* "$pkgdir/psp/include/GLES/"

	mkdir -m 755 -p "$pkgdir/psp/share/licenses/$pkgname"
	install -m644 LICENSE "$pkgdir/psp/share/licenses/$pkgname/LICENSE"
}

