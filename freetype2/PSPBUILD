# Maintainer: Wouter Wijsman <wwijsman@live.nl>
pkgname=freetype2
pkgver=2.10.2
pkgrel=1
pkgdesc="a freely available software library to render fonts"
arch=('mips')
url="https://www.freetype.org/"
license=('custom:Freetype' 'GPL2')
depends=('zlib')
makedepends=()
optdepends=()
provides=('freetype')
group=('all')
source=("https://github.com/pspdev/freetype2/archive/${pkgver}-psp.tar.gz")
sha256sums=('cda92a07646c0d1192a8d27b460c063a55a17bbf3961079bf12a6436bc652938')

build() {
    cd "$pkgname-$pkgver-psp"
    ./autogen.sh
    LDFLAGS="-L$(psp-config --pspsdk-path)/lib" LIBS="-lc -lpspuser" \
    ./configure --host psp --prefix=/psp --without-bzip2 --without-png --enable-freetype-config
    make
}

package() {
    cd "$pkgname-$pkgver-psp"
    make DESTDIR="$pkgdir/" install

    mkdir -m 755 -p "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 docs/LICENSE.TXT "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 docs/GPLv2.TXT "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 docs/FTL.TXT "$pkgdir/psp/share/licenses/$pkgname"
}

