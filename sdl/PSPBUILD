# Maintainer: Wouter Wijsman <wwijsman@live.nl>
pkgname=sdl
pkgver=1.2.15
pkgrel=1
pkgdesc="a library designed to provide low level access to audio, input, and graphics hardware"
arch=('mips')
url="http://www.libsdl.org/"
license=('GPL2.1')
depends=('pspirkeyb')
makedepends=()
optdepends=()
group=('all')
source=(
    "http://www.libsdl.org/release/SDL-${pkgver}.tar.gz"
    "SDL-${pkgver}-PSP.patch"
    "SDL_glfuncs.h.patch"
)
sha256sums=(
    'd6d316a793e5e348155f0dd93b979798933fb98aa1edebcc108829d6474aad00'
    'SKIP'
    'SKIP'
)

prepare() {
    cd "SDL-$pkgver"
    patch -p1 < ../SDL-${pkgver}-PSP.patch
    patch -p1 < ../SDL_glfuncs.h.patch
}

build() {
    cd "SDL-$pkgver"
    ./autogen.sh
    LDFLAGS="$LDFLAGS -L$(psp-config --pspsdk-path)/lib -L$(psp-config --psp-prefix)/lib -lc -lpspuser" \
    LIBS="$LIBS -lc -lpspuser" \
    ./configure --host=psp --prefix=/psp --enable-pspirkeyb
    make
}

package() {
    cd "SDL-$pkgver"
    make DESTDIR="$pkgdir/" install

    mkdir -m 755 -p "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 COPYING "$pkgdir/psp/share/licenses/$pkgname"
}

